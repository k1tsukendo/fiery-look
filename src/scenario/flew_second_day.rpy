label fl_day2:

    $ renpy.movie_cutscene("mods/flew/res/video/ov_perexod_dnya2.ogv")
    $ new_chapter(2, u"Огненный взгляд {p}День Второй")

    scene bg black with dissolve
    $ renpy.pause(1, hard=True)

    scene bg ext_square_sunset with dissolve2
    play ambience ambience_camp_center_day fadein 4

    $ renpy.pause(1, hard=True)
    $ sunset_time()
    $ persistent.sprite_time = 'sunset'

    play music music_list["two_glasses_of_melancholy"] fadein 4
    window show dissolve

    "Самое обычное летнее утро. {w}Ну, почти обычное."

    th "Черт."
    th "Как же болит голова. {w}Невыносимо. {w}Бегают тут. {w}Всякие. {w}Пропадите вы пропадом. {w}Орете ходите. {w}Всего-то немного позже лег, и на тебе. {w}Получите — распишитесь."

    "Опустив многострадальную голову вниз, я сидел на лавочке и массировал виски, пытаясь хоть как-то облегчить свои страдания."

    th "Жил себе спокойно, никого не трогал. {w}На тебе приключение, мил человек! {w}Зашибись путёвочка."
    th "Ох, когда же эта боль закончится, я от неё рехнусь скоро."
    th "Самое лучшее утро в моей жизни."
    th "И {b}никто{/b}, главное, не подойдет и не поможет."
    me "Пионер — всем ребятам пример."

    "Передразнил я вожатую."

    th "Ага, как же! {w}Взаимовыручка, твою мать!"

    "Не знаю, сколько бы так просидел, костеря всё вокруг на чем мир держится, но на плечо легла женская ладонь, а сама северянка присела рядом, пытаясь взглянуть мне в лицо."

    $ renpy.pause(1.5, hard=True)
    $ persistent.sprite_time = 'sunset'
    show sl sad sport at cleft with ov_trise
    $ renpy.pause (1)

    "Обычно веселая и жизнерадостная, она была явно обеспокоена. {w}Я попытался было в свойственной мне манере начать анализировать, чем это самое беспокойство было вызвано, но мой организм быстро напомнил мне, кто тут главный."
    show sl sad sport at center with moveinright

    sl "Что с тобой? {w}Тебе не хорошо?"
    me "Голова болит. {w}{b}Не видно{/b}?"
    
    "Я запоздало подумал, что это как-то слишком уж грубо, но звуки в моем речевом аппарате уже успели сложиться в слова, а те - в предложения и вылететь наружу."
    
    th "Приехали. {w}Взял, человека ни за что обидел. {w}Всё в этой жизни через твою-то мать!"
    show sl smile sport close at center with dissolve
    "Но, к моему удивлению, Славя не встала и не ушла. {w}Напротив, она придвинулась поближе, приобняла и стала ласково поглаживать меня по спине."

    sl "Подожди минуточку, я сейчас. {w}Никуда не уходи, я быстро."

    show sl smile sport at center with dissolve
    $ renpy.pause (0.3)
    hide sl smile sport with moveoutleft
    $ renpy.pause (0.5)

    "По правде говоря, последнее говорить было не обязательно. {w}В таком состоянии шататься по лагерю мне хотелось меньше всего на свете."

    window hide dissolve
    $ renpy.pause (2)
    show sl normal sport at center with moveinleft
    window show dissolve

    "Борясь с недомоганием, я и не заметил, как негласная помощница Ольги Дмитриевны вернулась обратно, принеся с собой пару таблеток и стакан воды."

    show sl normal sport at center with dissolve:
        ease 1.0 zoom 1.15
    $ renpy.pause (1)

    sl "Вот, держи. {w}Выпей, сейчас полегче станет."

    show sl normal sport at center:
        ease 1.0 zoom 1.0
    $ renpy.pause (2)

    "Я кое-как принял лекарство. {w}Не знаю, самовнушение или нет, но боль начала стремительно утихать. {w}Обрадовавшись этому, в голове снова засуетились и забегали случайные мысли и мыслишки."
    th "Кажется, полегчало. {w}Не до конца, но теперь вполне терпимо. {w}Даже сносно, я бы сказал."

    show sl smile sport at center with dissolve

    sl "Как себя чувствуешь?"
    me "Лучше, намного лучше. {w}Спасибо тебе, Славя."

    "Впервые за сегодняшнее утро я улыбался. {w}Северянка ответила мне тем же, крутя в руках пустой граненый стакан."
    sl "Может, давай тебя до домика провожу, ляжешь, отдохнешь?"

    "Я поморщился."

    th "Ой нет, с вожатой потом объясняться, лучше сразу пристрелите."
    "Славяна истолковала мои изменения в мимике иначе."

    show sl smile2 sport at center with dissolve

    sl "Ну-ну, не храбрись, здоровье беречь надо."
    me "Да-да, а то коммунизм некому строить будет."

    show sl laugh sport at center with dissolve_fast

    "Девушка беззаботно рассмеялась."

    sl "Вот уж не думала, что ты такой сознательный гражданин. {w}Но, может, всё-таки?…"
    me "Нет, не стоит, я лучше тут воздухом подышу."

    show sl normal sport at center with dissolve

    "Славя вздохнула, но спорить не стала."
    sl "Но если что…"
    me "Сразу к Виоле, знаю, не маленький."

    show sl smile sport at center with dissolve

    "Белокурая пионерка иронично хмыкнула."
    sl "Не маленький-то — не маленький, а сидел на лавочке и терпел."
    th "Судьба у меня такая. {w}Сидеть и терпеть."

    "Она с любовью погладила меня по плечу."
    sl "Эх, мальчики…"
    "И чуть серьезней добавила."

    show sl normal sport at center with dissolve

    sl "Сень, боли в голове это серьезно, не выжидай так больше, ладно?"
    "И, чуть помедлив, наигранно торжественно добавила."

    show sl smile sport at center with dissolve

    sl "А то с такими делами светлого будущего нам не видать!"

    show sl surprise sport at center with dissolve_fast

    "Внезапно девушка задумалась, словно что-то вспомнила."
    sl "Ох, я и забыла совсем! {w}Сень, у тебя часов с собой нет? {w}Не знаешь, сколько времени?"
    th "Эм, есть. {w}На мобильнике. {w}Вопросы сейчас будут… {w}Ладно, придумаю сказочку какую-нибудь. {w}Человек помог, некрасиво вот так отказывать."
    "Глупо улыбаясь, я потянулся к левому карману, натужно делая вид, что вещица, которую достаю из шорт, вполне себе обычная, и удивляться тут нечему. {w}Надо ли говорить, что делал я этим только хуже?~"
    "Но, к моему удивлению, Славя никак не изменилась в лице, не стала задавать неудобных вопросов, на которые внятных ответов, как вы понимаете, у меня не было."
    me "Семь сорок два."

    show sl smile sport at center with dissolve

    sl "Спасибо."

    stop music fadeout 3
    $ renpy.pause (0.5)
    hide sl smile sport with ov_trise

    "Северянка еще раз благодарно улыбнулась, окинула меня взглядом, и, удовлетворившись увиденным, убежала по своим делам."

    th "Фух, пронесло! {w}До линейки еще далеко. {w}Посижу немного, и пора собираться."
    th "Хотя, куда мне идти? {w}Неизвестное место, неизвестные люди. {w}Что я вообще тут делаю? {w}Ладно бы одно это, так никто ж нормально ответить не может, который год сейчас, хотя бы. {w}Начнешь расспрашивать кто откуда, где учится, чем увлекается — опять какие-то туманные фразы. {w}Вот и вливайся в этот «дружный» коллектив как умеешь."
    th "А социализации у меня как мозгов у хлебушка. {w}Опять разворчался. {w}Нужно себя пересиливать, иначе точно решат, что со мной не то что-то и сдадут в соответствующие органы. {w}Для опытов. {w}Шутки шутками, но лучше действительно держать себя в руках, а то в следующий раз подойти может не Славя вовсе."

    $ renpy.pause (1)

    me "Красивая у неё попа, всё-таки."
    th "Я это вслух сказал!?"
    "Я опасливо оглянулся, убеждаясь, что вокруг никого нет."

    window hide
    $ renpy.pause (1)
    play music ov_rengoku_teien_yume_akari fadein 3
    $ renpy.pause (1)
    window show dissolve

    th "Фух, пронесло! {w}Так, пора прекращать до такой степени себя накручивать, то точно уеду в санаторий с мягкими стенами. {w}В общем и целом, если кто и услышит, то ничего страшного."
    th "В конце концов, девушкам же приятно, когда им говорят, что у них попа красивая. {w}Ну, я так предполагаю. {w}Ладно, если что, будем выкручиваться. {w}В конечном счете, мне в моей ситуации ничего другого не остается, кроме как ждать."
    th "Хотя чего именно? {w}Чего я жду? {w}Что именно станет тем самым событием, застав которое я скажу: «эгей, вот оно!»? {w}И что буду делать когда момент настанет. {w}Куча вопросов. {w}И чем больше я пытаюсь узнать, тем больше их становится. {w}А ответов. {w}Нет их, ни одного. {w}Ну и что теперь делать?"
    me "Ха, еще один вопрос, да?"

    "Последнюю фразу я раздраженно буркнул себе под нос."

    $ renpy.pause (1)

    "Посидев так еще немного, привычно ища быстрого и простого решения сложной проблемы, я мысленно махнул рукой и вальяжно развалился на скамейке, благоразумно решив не забивать голову раньше времени, решая задачи по мере их поступления. {w}Да и то, если они критичны. {w}В моих глазах, по крайней мере."

    th "Как бы вожатой не попасться, а то чует мое помолодевшее сердечко, нагрузят меня чем-нибудь очень полезным для развития во мне настоящего пионера. {w}А мне оно вот вообще не сдалось."

    Character(u'...', color="708090", what_color="E2c778") "Семён!"

    "Вдруг меня кто-то откликнул и потряс за плечо. {w}Движения хоть и были решительными, но всё равно слишком ласковыми для мужских. {w}В голове моментально само собой возникло предположение." with hpunch
    "Сердце бешено забилось. {w}На секунду показалось, что это не оно колотится во мне с такой силой, что вот-вот выпрыгнет, а я сам часто-часто дрожу на ветру, удерживаемый на месте одним лишь пульсирующим живыми моторчиком."

    show dv normal sport at center with ov_trise
    $ renpy.pause (1)

    th "Хех, тоже в спортивной форме? {w}Спортсменки блин."
    me "О, привет, Алиса! {w}Рад тебя видеть! {w}Эмм, в смысле, скучно мне было. {w}Я вот сижу тут думаю о жизни. {w}Скажи, ты о ней никогда не думала? {w}О жизни. {w}Как о явлении, ну или о своей собственной…"

    "Боль в голове окончательно утихла, освобождая место для звенящей пустоты."
    th "Черт, что я несу? {w}Еще и тараторить начал как наша японка."

    "На слове «японка» мысли самовольно галопом поскакали в сторону размышлений о машинах из страны восходящего солнца."

    th "У меня точно беды с башкой. {w}Семен, что ты делаешь, сейчас она развернется и уйдет, посчитав за идиота. {w}Эм, погодите-ка, она продолжает стоять, еще и улыбается?"

    show dv laugh sport at center with dissolve_fast

    dv "И тебе не хворать. {w}А ты чего тут сидишь?"

    "Белая футболка, короткие шорты. {w}Спортивная форма отлично выделяла прекрасную фигуру Алисы. {w}Шаловливые мыслишки услужливо рисовали картины, быстро-быстро сменяющие друг друга все более и более откровенными."
    "Я замялся."

    show dv normal sport at center with dissolve

    dv "Ну? {w}Чего молчим? {w}Я тебя окликнула, а ты не отвечаешь. {w}Некрасиво, между прочим."
    me "Я задумался."

    "Сказал я с иронией."

    show dv grin sport at center with dissolve

    dv "Задумался он. {w}Такой молодой, а уже философ. {w}Ты чего-то бледный, ай-да со мной на пробежку!"
    me "Нет, спасибо, у меня голова болит."

    stop music fadeout 3
    $ renpy.pause (1.2)

    th "Идиот!"
    th "{b}Кретин!{/b}"
    th "{b}Придурок!{/b}" with hpunch
    th "Семен, что б тебя! {w}Какого черта ты творишь! {w}Тебя девушка на… {w}На… {w}На прогулку приглашает, а ты тут еще нос воротишь. {w}Мастер рукопашного боя, блин! {w}Бесконтактного."

    show dv angry sport at center with dissolve

    dv "{b}Пошли,{/b}{w}{b} кому я сказала! {/b}{w}{b}Голова у него болит{/b}{w}{b}, давай вставай{w}{b}, лентяй!{/b}"

    "Она хотела казаться грозной, но нотка беспокойства в интонация ее выдавала."

    play music music_list['that_s_our_madhouse'] fadein 3

    me "Ладно, хорошо-хорошо."
    me "Только я не в форме. {w}В смысле сейчас в форме, но не в той. {w}Я не физическую имею в виду, а то подумаешь еще, но и это тоже… {w}О чем это я?"

    show dv normal sport at center with dissolve

    dv "Как все запущено-то. {w}Иди уже переодеваться! {w}Короче, жду на пляже, даю тебе 5 минут."

    hide dv normal sport at center with dissolve

    "Вильнув вторыми девяносто, Алиса ушла."

    window hide dissolve
    stop music fadeout 3
    $ renpy.pause (1.5)
    play ambience ambience_camp_center_day fadein 5
    window show dissolve

    "Сознание все еще находилось в полусоображающем состоянии, поэтому управление на себя взял встроенный автопилот, отправив мое бренное тело в сторону нашего с Ольгой Дмитриевной домика."

    window hide dissolve
    $ renpy.pause (0.7)
    scene bg ext_houses_sunset at running with dissolve
    $ renpy.pause (1.3)
    scene bg ext_house_of_mt_sunset with ov_trise
    play sound sfx_knock_door2
    $ renpy.pause (0.5)
    window show dissolve

    "Постучавшись, я на автомате тут же потянулся к дверной ручке, но вовремя одернул себя."

    stop ambience
    $ prolog_time()
    scene image im.MatrixColor(get_image("cg/d2_mt_undressed.jpg"), im.matrix.desaturate())
    show prologue_dream
    with fla
    play ambience ambience_int_cabin_day

    "..." "Семён? {w}Стучаться надо! {w}А теперь вон!"

    window hide
    stop ambience
    $ sunset_time()
    scene bg int_house_of_mt_sunset with flash
    play ambience ambience_int_cabin_day
    window show dissolve

    th "Как раз тот случай, когда и стыдно и не очень. {w}В конце концов, Ольге Дмитриевне стесняться абсолютно нечего — она красивая женщина. {w}Насколько я могу судить о женской красоте. {w}Нет! {w}Без всяких «но» и «если». {w}Она красива, и это не обсуждается."
    th "Эх, если бы меня мотивировали заниматься общественно полезным трудом не обещанием сделать из меня примерного человека и пламенными речами о счастливом будущем, которое обязательно наступит завтра, а стриптизом от вожатой… "
    th "Надо будет у поваров тактично спросить, нет ли у них брома или чего-то такого. {w}Иначе я точно ли свихнусь. {w}Или на кого-то наброшусь. {w}Или придется бегать по ночам в кусты сбрасывать напряжение за день. {w}И не дай бог меня за этим кто-нибудь застукает. {w}Хоть в лес от стыда сбегай тогда."
    th "Не было печали. {w}Сидел один дома - страдал от одиночества, попал в лагерь полный красивых девушек — не знаешь куда девать свое либидо. {w}Жизнь — боль."

    "Подождав реакции на стук в дверь и не получив её, я вошел в домик. {w}Как мне и подумалось, он был пуст."
    "Я взял спортивную форму, висевшую на стуле. {w}Удачно, что её выдали ещё вчера вместе с пионерской формой."
    "Быстро одевшись, я поспешил на пляж."

    window hide dissolve
    scene bg black with ov_trise
    $ renpy.pause (0.5)
    play ambience ambience_boat_station_day fadein 4
    scene bg ext_beach_sunset with ov_trise
    show dv angry sport at center with dissolve
    window show dissolve

    dv "{b}Какой ты долгий!{/b}"
    me "{i}Подумаешь. {/i}{w}Ну что, газ в пол, жги резину?"

    show dv normal sport at center with dissolve

    dv "И звуковое сопровождение будет, как в детстве?"

    show dv laugh at center with dissolve_fast

    extend " Ну, тронулись, гонщик."
    "Мы рванули."

    stop ambience
    play music ov_ross_hardy_brian_white_coming_home fadein 3
    scene bg ext_beach_sunset at running with flash

    "Алиса бежала чуть впереди. {w}Естественно, мой взгляд проскользил по её спине вниз к двум женским округлостям, буквально раздевая их хозяйку в страстном порыве. {w}Я отвел взгляд в сторону и начал складывать в уме дроби."

    scene bg ext_houses_sunset at running with flash

    th "Бром, чертов бром, надеюсь ты есть в этой дыре. {w}Иначе я не выживу."
    "Через какое-то время мне стало легче и я даже мог относительно спокойно смотреть на Алису. {w}От этого на душе стало веселее."

    scene image im.MatrixColor(get_image("bg/ext_washstand_day.jpg"), im.matrix.tint(0.94, 0.82, 1.0)) at running with flash
    $ renpy.pause (0.6)

    "Мы пробежали умывальники."

    scene image im.MatrixColor(get_image("bg/ext_square_day.jpg"), im.matrix.tint(0.94, 0.82, 1.0)) at running with flash
    $ renpy.pause (0.6)

    "Площадь."

    scene image im.MatrixColor(get_image("bg/ext_clubs_day.jpg"), im.matrix.tint(0.94, 0.82, 1.0)) at running with flash
    $ renpy.pause (0.6)

    "Здания клубов."

    scene image im.MatrixColor(get_image("bg/ext_musclub_day.jpg"), im.matrix.tint(0.94, 0.82, 1.0)) at running with flash
    $ renpy.pause (0.6)

    "Музкружок."

    scene image im.MatrixColor(get_image("bg/ext_dining_hall_away_day.jpg"), im.matrix.tint(0.94, 0.82, 1.0)) at running with flash
    $ renpy.pause (0.6)

    "Столовую."

    scene image im.MatrixColor(get_image("bg/ext_path2_day.jpg"), im.matrix.tint(0.94, 0.82, 1.0)) at running with flash
    $ renpy.pause (0.6)

    "И, в итоге, вышли на тропинку в лесу."

    window hide dissolve
    scene bg black with ov_trise
    $ renpy.pause (1)
    scene cg ov_alisa_running with ov_trise
    $ renpy.pause (0.5)
    window show dissolve

    "Было довольно темно, скорее всего, из-за больших деревьев с густыми ветвистыми кронами, которые закрывали солнце."
    "Алиса бежала довольно быстро, по её уверенным движениям складывалось впечатление, что спорт ей не чужд."

    th "Еще бы, такую фигур{cps=3}...{/cps} {w}СТОЯТЬ, {b}СЁМА{/b}! {w}Обыкновенные дроби,{w=1} обыкновенные дроби,{w=1} обыкновенные дроби! {w}Так-то лучше."

    "Сколько бы я ни пытался, все равно не мог догнать рыжеволосую бестию. {w}В голове закралась шальная мысль."

    th "А, может, это хулиганистое очарование{w=1} - ведьма? {w}Хотя нет, глаза не зеленые… {w}Впрочем, она могла изменить их цвет. {w}Ведьмы, они коварные женщины. {w}И страстные любо… {w}Да что же это такое!"

    "Через некоторое время уже стало не до шаловливых мыслей, бок начал болеть. {w}Дыхание сбилось."

    me "Алис, {i}давай отдохнём{/i}? {w}Мы бежим минут так десять без остановки."

    "Запыхавшись, еле проговорил я."

    dv "Все, перегрев двигателя? {w}Ещё немного, ещё чуть-чуть, ещё рывок, он трудный самый!"

    "Прощебетала лисичка, переделывая на свой манер известную песню. {w}Кажется, ей нравится надо мной подтрунивать."

    play ambience ambience_boat_station_day fadein 4
    scene bg ext_beach_sunset at running with dissolve
    stop music fadeout 3

    "Судя по внутренним часам, мы бежали еще, по крайней мере, минут пять, пока не оказались на пляже. {w}Снова."

    scene bg ext_beach_sunset with dissolve

    me "Фу-ух."
    "Я с ходу повалился на песок и закрыл глаза."

    show blink
    $ renpy.pause (1)

    th "Бегать за девушками опасно для здоровья. {w}Правы были психологи из интернетов."
    "Вдруг я почувствовал внезапные холод и сырость и резко встал."

    play music ov_hans_zimmer_the_battle_ost_gladiator fadein 3
    with hpunch
    scene bg ext_beach_sunset
    show dv laugh sport
    hide blink
    show unblink
    $ renpy.pause (1)

    dv "Ты не ты - когда устал!"

    hide unblink

    "Подмигивая сказала Алиса."
    th "Вот же чертовка, ну я ей сейчас устрою!"

    "В голове заиграла воодушевляющая мелодия из фильма «Гладиатор». {w}Послышался шум жадной до крови и зрелища толпы. {w}А перед глазами возникла картина — мы на арене Колизея. {w}Благочестивые мужи и даже сам Император приветствуют нас! {w}Вообще, должно быть наоборот, но это моя фантазия, отстаньте!"
    "Подбадриваемый собственным воображением, я подошёл к реке и набрал в ладони воды. {w}Брызги, словно метательные снаряды, безжалостно полетели в Алису."

    show dv rage sport with vpunch

    dv "{b}Эй{/b}, {b}совсем страх потерял?!{/b}"
    me "{i}Битва{/i} только начинается."

    "Я начал обливать лисичку всё смелее, увеличивая натиск, опьяненный горячкой «боя»."

    show dv laugh sport:
        linear 0.5 xalign 0.8
        linear 0.5 xalign 0.2
        repeat

    "Но Алиса не терялась, она умело уклонялась от моих «выстрелов», да еще и успевала набирать в ладошки воды, и атаковать в ответ."

    $ renpy.pause (0.7)

    "Наша одежда насквозь промокла, но мы продолжали биться как истинные гладиаторы!"

    stop music fadeout 4
    window hide dissolve
    scene bg black with ov_trise
    $ renpy.pause (1.2)
    play music ov_ross_hardy_brian_white_14_hole_punch fadein 3
    scene bg ext_beach_sunset with ov_trise
    show dv normal sport2 at center with dissolve_fast
    window show dissolve

    "Мы настолько заигрались, что я не заметил, как…"
    "Из-за влаги, футболка Алисы начала просвечивается. {w}И{cps=3}...{/cps}"

    th "Она без лифчика…!?" with hpunch
    th "Да что-ж это такое!"

    "Я отвел глаза и остановился, приготовившись падать лицом вниз. {w}Если что, скажу, что люблю песочные маски по утрам."

    dv "Чего? {w}Уже сдался?"
    "Она осмотрела свою одежду и поняла что именно меня так смутило."

    show dv surprise sport2 at center with dissolve

    dv '{cps=3}...{/cps}'
    me "Что? {w}Я не виноват что ты без…"

    "Машинально, черт бы побрал эту привычку оживленно жестикулировать, руки потянулись к верхней половине моего торса, наглядно показывая, чего именно из белья не хватало на Алисе."
    "Акцентировать внимание на её груди было плохой идеей. {w}Мой организм отреагировал так, как и должен был."

    show dv rage sport2 at center with dissolve_fast

    dv "{b}Ах ты скотина!{/b}" with hpunch
    "Алиса резко подалась вперёд и подарила мне не сильную, но ощутимую аплеуху."

    play sound sfx_face_slap
    with flash
    with hpunch

    me "{b}Ай! {/b}{w}{b}За что?{/b}"
    "Мне было жутко стыдно. {w}Не знаю, за что именно. {w}Просто стыдно."

    stop music fadeout 3
    hide dv rage sport2 with dissolve

    "Девушка глубоко вздохнула, похоже она кое-как успокоилась, или, по крайней мере, попыталась."
    dv "Пойду переоденусь, жди здесь."

    "Тем временем Алиса скрылась."
    "Всё ещё находясь в ступоре, я так и остался стоять посреди пляжа. {w}Горячка спала, и я вновь стал выглядеть прилично. {w}Насколько прилично может выглядеть мокрый пионер."

    th "Приехали. {w}Ну и что теперь делать? {w}Я пытался, честно пытался. {w}Но не скажешь же ей о том, что у меня внутри творится когда воочию вижу женскую красоту, да еще и так близко."
    th "Ладно, что ни делается, всё к лучшему. {w}Зато весело было."

    $ renpy.pause (1)

    "Я беззаботно расслабился, и разлегся на песке."

    window hide dissolve
    scene bg ov_sky_sunset with ov_trise
    $ renpy.pause (1)
    window show dissolve

    th "Все, однако, относительно хорошо идет, даже слишком. {w}Словно я в сказке, Алиса - Красавица, а я – Чудовище. {w}Половое, хех."
    th "Мне действительно было весело. {w}{i}С ней{/i}."
    th "Я забыл обо всём на свете. {w}Забыл, что я в этом гребаном пионерлагере, забыл, что я, мать вашу, в советском прошлом."
    th "Забыл, что я каким-то образом переместился в пространстве и времени из зимы в лето, из своего родного города в… {w}куда-то….Забыл, что моя любимая рыбка Полли, скорее всего, уже сдохла."
    th "Хотя, говорят, что рыбки могут без еды жить около двух недель, так что, ещё не все потеряно. {w}{b}Однако{/b},{w} {b}есть вопрос{/b}{w} – {b}на сколько долго я останусь здесь? {/b}{w}{b}Хочу ли я назад? {/b}{w}{b}Что будет если закончится смена? {/b}{w}{b}Если она закончится конечно…{/b}"
    th "Как бы там ни было, что случилось, того не вернуть. {w}Будем надеяться, что слухи окажутся правдой, и никто не пострадает."

    "Я, незаметно для себя, улыбнулся."

    th "Самое интересное, всё происходящее со мной уже воспринимается как вполне нормальное явление, словно каждый, или почти каждый, проходил через подобное. {w}А вообще, грех жаловаться. {w}Лето, отдых. {w}Короткие юбочки. {w}А под ними…"

    $ renpy.pause (1)
    play sound sfx_face_slap

    "Я дал себе пощечину."
    with flash_red

    th "Будешь знать, извращенец!"

    "Долго ли, коротко ли, я лежал на песке, широко раскинув руки-ноги, напоминая собой морскую звезду, купаясь в лучах утреннего солнца, убаюкивающем, коварном полудреме и воздушной, словно зефир, неге."

    "Спокойный шум легкого ветерка и речных волн обострили мой, пытающийся их уловить, слух."

    "Со стороны лагеря послышались шаги, приглушаемые рассыпающимся под ногами песком. {w}Самым очевидным, для меня, предположением было то, что это Алиса."

    dv "Я вернулась."

    "Как и положено в приличном обществе, я, хоть и нехотя, поднялся, встречая, девушку."

    scene bg ext_beach_sunset with dissolve
    show dv normal pioneer at center with dissolve_fast

    me "Надеюсь, на этот раз, ты в лифчике."

    show dv rage pioneer at center with dissolve_fast

    dv "{b}Тебе не хватило?!{/b}"

    play sound sfx_body_bump
    scene bg black with flash
    play music ov_ross_hardy_brian_white_06_lady_luck fadein 2

    "Рыжая хулиганка подскочила ко мне, сделала подножку и повалила лицом вниз, а сама уселась сверху, заломав мои руки так, чтобы у меня не было возможности сопротивляться. {w}По правде говоря, не очень и хотелось."

    "Действительно чувства настолько обострились, но мне показалось, что я ощущал жар алисиных бедер, которыми она поджала меня с обоих сторон."

    dv "Совсем уже охамел, извращуга!" with hpunch

    extend " А ну, проси прощения!"

    th "По-моему, она немножко не понимает."

    "Я заулыбался, стараясь поднять голову, чтобы не надышаться песком."

    th "Хорошо, что не на спине лежу, то получилось бы совсем неловко. {w}Немного больно, конечно, но как есть. {w}Эх, Алиска-Алиска, знала бы ты как мне сейчас хорошо. {w}Что это за наслаждение, когда впервые в жизни тебя крепко обнимает, обвив ногами, такая красавица."

    "Разъяренная пионерка, не дождавшись ответа, сильнее скрутила мне запястье и наклонилась вперед. {w}Спина почувствовала прикосновение девичьего животика и, кажется, груди. {w}Сердце бешено заколотилось, сознание порывалось улететь куда-то в райские сады, и мне потребовалось приложить немало усилий, чтобы удержать себя в руках."

    dv "Ну?!"

    "С нажимом сказала она, выдохнув свою фразу почти мне на ухо. {w}От такого эротизма волна возбуждения снова прокатилась по всему телу."

    th "Господи, Алиса. {w}Ты сводишь меня с ума! {w}Нет, надо это всё прекращать. {w}Это плохо кончится, это неправильно, так нельзя."

    "Меня разрывали надвое порядочное воспитание и первобытная похоть, ситуация была настолько абсурдной, что я нервно посмеялся."

    stop music fadeout 3

    dv "Да иди ты!"

    window hide dissolve
    scene bg ext_beach_sunset with dissolve
    $ renpy.pause (1)
    window show dissolve

    "Рыжик встал с меня, освободив от болезненного захвата. {w}Я поднялся следом, отряхиваясь от налипшего песка."

    play music ov_ross_hardy_brian_white_tuneland fadein 4

    me "Ладно, прости, я не хотел тебя обидеть. {w}Пойдём, то нас сейчас обыщутся."

    show dv normal pioneer at center with dissolve

    dv "Дай подумать{cps=3}...{/cps}"

    $ renpy.pause (1)
    show dv laugh pioneer at center with dissolve

    dv "Уговорил, принимаю твои извинения, извращенец, пошли."

    show dv smile pioneer at center with dissolve_fast

    extend " Кстати, ты сам переодеться не хочешь?"

    "Судя по ехидству в голосе, хорошее настроение начинает возвращаться к ней."

    me "Точно! {w}Ты иди, я догоню."

    dv "Погоди, куда собрался? {w}Линейка кончилась уже, наверняка. {w}Иди сразу в столовую, встретимся там."

    "Я кивнул и пошел в сторону моего с Олей домика."

    window hide dissolve
    stop ambience fadeout 2
    stop music fadeout 3
    scene bg black with ov_trise
    $ renpy.pause (1.5)
    play sound sfx_medpunkt_door_open
    scene bg int_house_of_mt_sunset with dissolve
    $ renpy.pause (1)
    play sound sfx_close_door_campus_1
    $ renpy.pause (0.7)
    scene bg black with dissolve
    $ renpy.pause (0.5)
    play ambience ambience_camp_center_day fadein 4
    scene bg ext_houses_sunset with dissolve
    window show dissolve

    "Переодевшись, я почти летел над землей."

    scene bg ext_dining_hall_near_sunset with dissolve

    "Мгновение, и вот уже стою возле столовой."

    window hide dissolve
    scene bg ext_dining_hall_near_sunset:
        xalign 0.45 yalign 0.45
        ease 1.5 zoom 1.25
    $ renpy.pause (2)
    show dv normal pioneer at center with dissolve
    $ renpy.pause (0.5)
    window show dissolve

    dv "Забыла сказать, Дмитриевну не видел?"

    "Не знаю почему, но в этот момент я больше любовался этой злюкой, чем слушал её."

    $ renpy.pause (1)
    show dv angry pioneer at center with dissolve_fast

    dv "Ау, есть кто дома?"

    "Рыженькая пионерка водила ладонью у меня перед глазами."

    me "А? {w=0.8}Да."

    dv "Что «да»? {w}Вожатой не попадись говорю, мы линейку пропустили, не забыл?"

    th "По правде говоря, да. {w}В смысле, забыл."

    me "Помню, конечно."

    show dv grin pioneer at center with dissolve

    dv "Отлично. {w}И вот еще что…"

    show dv shocked pioneer at left with moveinright
    show mt angry pioneer at right with moveinright
    play music music_list["scarytale"] fadein 1

    mt "Персунов! {w}Двачевская! {w}Вот вы где! {w}Почему вас на линейке не было, потрудитесь объяснить! {w}Где вас нелегкая носила всё это время?!"

    th "Ну да, ну да, так раз вовремя…"

    me "Бегали."

    "Не моргнув и глазом, я сказал первое, что пришло на ум. {w}Ольга Дмитриевна явно не ожидала такого ответа. {w}Алиса, к слову, тоже. {w}Она стояла и удивленно хлопала глазами. {w}Впрочем, продолжалось это недолго, спустя долю секунды недоумение на лице сменилось знакомым насмешливым прищуром и лукавой ухмылкой."

    dv "Мы{cps=3}...{/cps} {w}Забегались, потеряли чувство времени. {w}А сигнал на линейку не услышали, вот и опоздали."

    th "По-моему, она решила, что у меня какой-то хитрый план. {w}Что я тут могу сказать. {w}Иногда быть идиотом, неся отборную чушь, выгодно."

    mt "Это ничего не меняет. {w}Вы оба наказаны. {w}Алиса, после завтрака отправляешься к Славе, поможешь ей."

    show dv angry pioneer at left with dissolve_fast

    "Алиса цокнула."

    mt "А ты, Семен — к кибернетикам, разберете кладовку. {w}И только попробуйте мне поотлынивать, до конца смены будете картошку в столовой чистить! {w}Всё, идите завтракать."

    stop ambience fadeout 2
    stop music fadeout 2
    window hide
    scene bg ext_dining_hall_near_sunset:
        xalign 0.3 yalign 0.55
        ease 1.2 zoom 1.35
    $ renpy.pause (1.5)
    scene bg int_dining_hall_people_day with dissolve
    play ambience ambience_dining_hall_full
    $ renpy.pause (0.5)
    $ day_time()
    $ persistent.sprite_time = 'day'

    "Алиса схватила меня за запястье и потащила в сторону Ульяны, которая очень даже уютно устроилась за последним столиком."

    mt "За разными столиками!"

    "В голосе вожатой было столько металла, что желание ослушаться даже такого самодурного, на мой взгляд, приказа не было. {w}Я отнял руку и пожал плечами, Алиса кивнула мне в ответ и убежала к подружками."

    th "Мне интересно, спустя сколько дней вожатая начнет практиковать методики воспитания, описанные в Цельнометаллической оболочке? {w}Это уже ни в какие рамки! {w}Ну правда, и так из-за ерунды заставила батрачить, считай, пол дня, так еще и поесть вместе не даёт. {w}Концлагерь какой-то. {w}А ведь ещё вчера вечером мы с ней мило болтали и пили чай."

    th "Биполярное расстройство оно такое, да."

    "Выбравшись из череды раздумий, мне ничего не оставалось, как пойти к раздаче."

    scene bg black with dissolve
    $ renpy.pause (1)
    scene bg int_dining_hall_people_day with dissolve

    "Взяв с недовольной миной поднос, я сел за ближайший свободный столик. {w}Еда особо не порадовала: пюре, котлеты и чай."

    th "Ладно, повара тут ни причем. {w}Всё эта. {w}Чтоб ей не икалось."

    Character(u'...', color="708090", what_color="E2c778") "Можно?"

    "Раздался мужской голос."

    "Это был  {i}Сережа{/i}. {w}В простонародье Электроник. {w}Настоящий{w}, по его словам."

    me "Располагайся. {w}Чувствуй себя как дома."

    show el normal pioneer at center:
        xoffset -1500
        ease 1.5 xoffset 0.0
    $ renpy.pause (2)
    show el normal pioneer at center:
        parallel:
            ease 1.0 ypos 0.07
        parallel:
            ease 0.75 zoom 1.05
            ease 0.5 zoom 1.0
    $ renpy.pause (2.5)

    "Блондин выложил с подноса принесенный завтрак и сел за стол."

    el "Тоже не хочешь есть?"

    th "Похоже, скрыть свое недовольство от посторонних не получилось."

    me "Ага. {w}А почему “тоже”?"

    show el smile pioneer at center with dissolve:
        ypos 0.07 zoom 1.0

    el "Да мне, если честно, не особо нравится здешняя стряпня. {w}Что-то из сладкого давали только раз, и то, это было в самый первый день."

    me "Обижаешь поваров, а-яй."

    show el grin pioneer at center with dissolve_fast:
        ypos 0.07 zoom 1.0

    "Сергей саркастически ухмыльнулся."

    th "Как-то мне не очень этот разговор нравится. {w}Хоть еда и не из ресторана, но жить можно. {w}В конце концов, люди старались. {w}Или дело не в еде?"

    hide el grin pioneer with dissolve

    "Электронник опустил голову в тарелку, молча укладывая в себя еду."

    "Глядя на это, внутри словно что-то щелкнуло, и мне вдруг захотелось подбодрить человека, дать ему возможность выговориться."

    me "Слушай{cps=3}...{/cps}."

    show el normal pioneer at center with dissolve:
        ypos 0.07 zoom 1.0

    el "…?"

    "Кибернетик поднял на меня глаза."

    me "Ты чего такой грустный? {w}Случилось что? {w}Дело же не в сладостях, верно?"

    $ renpy.pause (1.5)

    "Сергей некоторое время колебался, но потом с грохотом бросил вилку на стол и отодвинул тарелку с завтраком."

    play sound sfx_table_hit

    show el serious pioneer at center with dissolve_fast:
        ypos 0.07 zoom 1.0
    play music music_list ["heather"] fadein 1

    el "Надоел он мне, сил моих больше нет! {w}Видеть этого робота не могу, хочется взять его и разломать к чертовой бабушке!" with hpunch

    me "Так зачем тогда с ним возишься?"

    el "Да не моя это идея!"

    "Кибернетик оживленно жестикулировал."

    el "Шурика. {w}Мы дружим, вместе учимся, вместе в кружок робототехники и электроники ходим."

    "На нас стали посматривать. {w}Увидев это, Электроник вздохнул, и, успокоившись, взял вилку в руку, пододвинул обратно уже подостывший завтрак и принялся есть, параллельно продолжая рассказ."

    show el normal pioneer at center with dissolve:
        ypos 0.07 zoom 1.0

    el "Вот и сюда решили ехать вместе. {w}Он этим роботом еще до приезда в лагерь заболел. {w}Там сложная история, в общем, попросил помочь. {w}А я, ну друг, всё-таки. {w}Ну не смог отказать. {w}Думал, запчастей тут нет, помучается-помучается, и бросит."

    me "Но что-то пошло не так, да?"

    "Я хотел было отругать себя за не вовремя ввинченный сарказм, но кибернетик на него никак не отреагировал."

    show el upset pioneer at center with dissolve_fast:
        ypos 0.07 zoom 1.0

    el "Что не так, так это точно. {w}Злой стал, дерганый. {w}Не узнаю его совсем. {w}Хотел к медсестре сводить, мало ли что, так он такую истерику закатил!"

    el "Обвинил в предательстве, мол, мне просто надоело с его железкой возиться, вот и я пытаюсь от него избавиться, чтобы не донимал. {w}А мне и правда надоело. {w}В пыли бумажной сидеть. {w}И так почти всё время канифолью этой дышишь."

    show el normal pioneer at center with dissolve:
        ypos 0.07 zoom 1.

    el "А на улице лето, жара, пионерки{cps=3}...{/cps}."

    stop music fadeout 5

    "Электроник засмотрелся в окно, но, всё же, продолжил."

    el "Ехал сюда отдохнуть, отвлечься от учебы, рутины домашней. {w}А оно вон как вышло. {w}Вместо отдыха — работа, вместо друга — псих."

    play music ov_ross_hardy_brian_white_mad_hatter fadein 2
    show sh normal at fleft with moveinleft

    "Неожиданно, в нашу беседу влился сам Александр. {w}Он, в отличие от своего товарища, был в приподнятом настроении и насвистывал себе какую-то до боли знакомую мелодию. {w}Слегка пританцовывая, кибернетик приземлился на свободное за нашим столиком место."

    show el normal pioneer at right with moveinleft:
        ypos 0.07 zoom 1.0
    show sh normal pioneer at left with moveinleft:
        parallel:
            ease 1.0 ypos 0.07
        parallel:
            ease 0.75 zoom 1.05
            ease 0.5 zoom 1.0

    sh "Привет, Семен. {w}А я тебя ищу везде."

    "Последнюю фразу он адресовал Электронику."

    sh "Захожу — тебя нет нигде, дверь не заперта. {w}Пока ключи искал, пока запер, чуть не опоздал. {w}Ты закрыть, если отойти надо, не забудь в следующий раз, а то мало ли, испортят нашу работу или вообще украдут."

    "При упоминании злополучного механизма, Электроник с силой сжал вилку и буркнул под нос что-то вроде: «Да кому нужно это барахло»."

    "Я решил разрядить обстановку и увести разговор в другое русло. {w}Попытался, по крайней мере."

    me "Слушай, Шурик, а ты только роботами интересуешься, или еще чем?"

    th "Ох, не лучшая попытка, ох, не лучшая."

    show sh smile pioneer at left with dissolve:
        ypos 0.07 zoom 1.0

    "Кибернетик поправил очки и внимательно посмотрел на меня."

    th "Да, жутковатое зрелище."

    sh "Надумал-таки вступить в наш клуб?"

    "Я саркастически улыбнулся и отрицательно покачал головой."

    me "Нет, просто для поддержания разговора. {w}Меня к вам на помощь отправили, а я хочу знать с кем работаю — профессиональная привычка."

    "Шурик хмыкнул."

    sh "Да нет, много чем. {w}Мне вообще наука как явление интересно. {w}И техника. {w}А ты чем?"

    th "Ой, этого я не продумал. {w}Ладно, сейчас сориентируемся."

    me "Я? {w}Я больше гуманитарий, книжки читаю, на звезды смотрю. {w}На велосипеде люблю кататься."

    th "Последнее, в общем-то правда. {w}Было несколько лет назад. {w}Так что, технически, даже и не враньё."

    show sh upset pioneer at left with dissolve:
        ypos 0.07 zoom 1.0

    "Шурик погрустнел."

    sh "Я тоже… {w}Одно время им увлекался."

    me "Так а почему перестал? {w}По-моему, каждый парень любит в железках ковыряться, нет?"

    sh "Разонравилось."

    "Кибернетик явно не желал продолжать эту тему."

    me "Значит, наука и техника. {w}Когда увлечений много — это хорошо. {w}Самому с собой не скучно."

    show sh serious pioneer at left with dissolve:
        ypos 0.07 zoom 1.0

    th "Как-то разговор не клеится."

    sh "Да не особо. {w}Очень мешает институт выбирать. {w}Сначала в один хочешь, потом в другой. {w}Голова уже кругом."

    "Речь Шурика была всё такой же бесцветной."

    th "Это он так из-за поступления такой дерганый? {w}Вообще, не удивительно, как вспомню свои экзамены, о-ох!"

    me "Ну ты так не переживай, еще же…"

    th "Черт, сейчас проколюсь! {w}В каком же они классе? {w}У них их десять, или одиннадцать? {w}Играй теперь в угадайку!"

    me "…много времени. {w}Только не говори, что уже расписал всю жизнь наперед — где работать, на ком жениться, сколько детишек заводить."

    "Я нервно захихикал. {w}Да, с юмором у меня не очень. {w}Внезапно Шурик зло на меня зыркнул и…"

    show sh rage pioneer at left with dissolve_fast:
        ypos 0.07 zoom 1.0

    sh "Да как вы меня достали уже!" with hpunch

    show sh rage at left with dspr
    $ renpy.pause (0.2)
    hide sh rage pioneer with moveoutleft

    "Он выскочил из-за стола и, громыхнув стулом, чуть-ли не зашвырнув его на прежнее место, выбежал из столовой."

    show el normal pioneer at center with moveinright:
        ypos 0.07 zoom 1.0

    th "Что-то все сегодня нервные. {w}Видимо, день такой."

    hide el normal pioneer with dissolve

    "Тем временем я с Электроником сдали грязную посуду и отправились выполнять задание вожатой."

    window hide dissolve
    stop ambience fadeout 2
    stop music fadeout 3
    scene bg black with dissolve2
    $ renpy.pause (2.5)
    play ambience ambience_clubs_inside_day fadein 3
    scene bg int_clubs_male2_night with dissolve2
    $ renpy.pause (0.5)
    window show dissolve

    "Кладовка оказалась завалена всяким барахлом. {w}И если пару испорченных патифонов, пустую банку из-под краски, засохший клей и кучу поломанных радиодеталей я еще могу понять, да даже кусок засохшего хлеба и бутылку из под водки, то вот как там оказались промасленные розовые женские трусики и четыре черных дырявых носка, для меня осталось загадкой."

    "Мы работали не покладая рук, но даже спустя полтора часа, конца и края этого мусорного безумия не было видно."

    me "Хорош твой друг, однако. {w}Обиделся на какую-то ерунду, сбежал, а теперь и носа не показывает. {w}Сдается мне, бросил он нас тут двоих, лишь бы каморку вашу не чистить, да и"

    "всё."

    "Сергей повертел в руках очередную пустую банку с непонятной засохшей белой субстанцией и бросил в мешок к остальному мусору."

    show el normal pioneer  at center with dissolve

    el "Семен, скажи, ты тайны хранить умеешь?"

    me "{b}Разумеется.{/b}"

    "Тут же ответил я. {w}И не соврал. {w}Другое дело, что разбалтывать за всю жизнь было нечего и некому."

    play music ov_you_re_dam_funny_7dl fadein 4

    el "Ладно, понадеюсь на твое честное слово. {w}В общем, Саша, он парень довольно стеснительный."

    me "Да и ты тоже не душа компании."

    th "Ой, да что ж с меня сегодня всё лезет! {w}Точно с Луной что-то не так, не иначе."

    "Я уже начал подыскивать себе оправдание, но Электроник не стал заострять внимание на моём не слишком уместном замечании."

    el "А теперь представь масштаб бедствия, если это даже я тебе говорю. {w}Помнишь, я рассказывал об одержимостью роботами. {w}Так вот. {w}Перед этим Саша за одной девочкой из нашей школы пытался ухаживать. {w}Как умел. {w}Давал списывать, помогал с домашкой и всё такое."

    me "{i}Допустим{/i}."

    show el serious pioneer at center with dissolve

    el "Кончилось всё тем, что когда он, наконец, предложил ей встречаться, она его перед всем классом очкастым ботаником назвала."

    me "Оу, можешь не продолжать, я понял что там дальше было. {w}А ты пробовал с ним поговорить, объяснить там, мол, она просто дура, что найдет еще себе хорошую девушку?"

    show el normal pioneer at center with dissolve

    el "С этим проблема."

    me "С объяснением?"

    el "С девушками. {w}Как мне удалось выяснить, сделала она это потому, что она не первая, кто Шурику отказывает. {w}А началось всё с того, что Саша за одноклассницей одной бегал. {w}Он как раз тогда велосипедами увлекался, хотел на соревнованиях попробовать выступать. {w}Вот из-за этого и получил отказ."

    me "Не понял прикола. {w}Девчонки же любят спортсменов."

    el "Да, но Саша внешне на него не тянет. {w}Всегда опрятный, вежливый. {w}Ну, ты понимаешь."

    me "И одно с другим не вяжется. {w}Да, представляю себе ситуацию. {w}Так а как это с последней девочкой связано?"

    show el serious pioneer at center with dissolve_fast

    el "Та одноклассница не самая красивая была. {w}Честно говоря, сам не знаю, что в ней Санёк нашел."

    me "Ну и?"

    show el normal pioneer at center with dissolve

    el "Видимо, эта последняя и все предыдущие решили, что чем они хуже первой и тоже дали от ворот поворот."

    me "Женская логика. {w}А в другую школу перевестись? {w}Переехать?"

    el "Городок маленький, школа всего одна, а переезд… {w}Он говорил с родителями, но те ни в какую. {w}Сказали тоже, что и ты, найдешь еще себе невесту, не забивай голову."

    me "Дела{cps=3}...{/cps}."

    show el upset pioneer at center with dissolve

    el "С тех пор он и стал замыкаться в себе, дома торчать целыми днями, еще больше в учебу ударился. {w}Наверное, решил посвятить жизнь науке."

    hide el upset pioneer with ov_trise

    "Я опасливо украдкой осмотрел лежащего на столе робота."

    th "Наука, значит. {w}Сдается мне, наличие женских первичных половых признаков у этого чуда инженерной мысли не случайно."

    "Разговаривать больше особо не хотелось, и мы, погруженный каждый в свои мысли, продолжили уборку."

    window hide dissolve
    stop music fadeout 3
    stop ambience fadeout 3
    scene bg black with ov_trise
    $ renpy.pause (1.5)
    scene bg ext_dining_hall_near_day with ov_trise
    play music ov_carefree fadein 3
    window show dissolve

    "Сигнал на обед, как ему и положено, прозвучал за пятнадцать минут до начала самого обеда. {w}Весь мусор из подсобки уже лежал в мешках и дожидался своей участи, а я, вычистившийся и вымывшийся отдыхал в тенечке под навесом у столовой."

    th "Фух, упахался. {w}Не, в баню! {w}Лучше на эту линейку не опаздывать. {w}Кстати, о бане, ни разу в жизни там не был, неплохо бы сходить искупаться разок, прочувствовать все её прелести. {w}Прелести. {w}И что, ни одной, даже самой завалящей, пошлой мыслишки не возникнет? {w}Наверное, в лесу что-то сдохло. {w}Либо я настолько устал. {w}Одно из двух."

    "Определив задачу минимум на ближайшие дни, я уже собрался заходить внутрь, как вдруг заметил быстро идущую ко мне компанию девчонок, две из которых были рыжими, а волосы третей имели цвет аквамариновой волны."

    show dv normal pioneer at left with dissolve_fast
    show us grin pioneer at right with dissolve_fast
    show mi normal pioneer at center with dissolve_fast
    $ renpy.pause (1)
    show mi smile pioneer at center with dissolve

    "Приблизившись на расстояние эффективного огня, Мику, не дав сказать и слова, стала поливать меня потоком своих эмоций."

    mi "Семочка, привет! {w}А я тут Алису провожаю, её же Ольга Дмитриевна к Славе на исправительные работы отправила."

    "Алиса громко фыркнула."

    mi "Мне скучно было в музкружке одной сидеть, а тут Ульяна прибегает, говорит, подругу спасать надо, а как же не спасать, какая же я тогда ей подруга. {w}Вот и пошла. {w}Я Ульяну спрашиваю, а в чем помощь нужна, а она говорит, нужно Славю отвлекать, чтобы меньше работали, её же, Алису то есть, только до обеда наказали."

    mi "Я спрашиваю, а как, а Ульяна говорит, гитару возьми, развлекать нашу северянку будешь, а я ей говорю, гитару не возьму, а она…"

    me "Стоп-стоп-стоп! {w}Мику, машингевер мой ненаглядный, помедленнее, иначе я не выживу."

    show mi shocked pioneer at center with dissolve

    "Мику округлила глаза."

    mi "Кто?"

    me "Пулемет такой есть."

    show dv grin pioneer at left with dissolve_fast

    dv "Сень, ну кто девушку какой-то железякой называет, а?"

    show mi sad pioneer at center with dissolve
    show dv smile pioneer at left with dissolve_fast

    me "И ничего не железяка, а очень известная стрелковая система, скорострельная и безотказная."

    show mi shy pioneer at center with dissolve
    stop music fadeout 3

    "Мику покраснелa."

    th "Чего это она? {w}Ну да, она быстро разговаривает… {w}Ой."

    me "В смысле, не ломается."

    "Мику совсем смутилась, закусила губу и сложила руки перед собой."

    th "По-моему, я делаю только хуже."

    me "То есть надежная. {w}Не подведет."

    show dv grin pioneer at left with dissolve

    "Алиса уже не скрывала свою фирменную, слегка надменную улыбку."

    dv "Да мы поняли, поняли. {w}Может, пойдем уже есть, а то сейчас вся орава набежит, будем пол года свою очередь отстаивать."

    hide us grin pioneer with moveoutright

    "Ульяна, не дожидаясь ни чьего согласия, с нечленораздельным воплем бросилась в дверной проем. {w}Остальным же ничего не оставалось, как пойти следом за ней."

    window hide dissolve
    stop music fadeout 3
    scene bg black with ov_trise
    $ renpy.pause (1.5)
    scene bg int_dining_hall_day with ov_trise
    play music ov_v_flower_close_to_you fadein 4
    window show dissolve

    "Взяв свои порции, мы отыскали самый лучший столик. {w}Я с удивлением обнаружил, что все в нашей компании проголодались и никого не тянет на разговоры. {w}Доев и сдав грязную посуду в мойку, мы вышли наружу, продравшись через толпу галдящих пионеров, только-только спешащих наполнить свои желудки."

    window hide dissolve
    scene bg black with dissolve
    $ renpy.pause (1)


    scene bg ext_dining_hall_away_day with dissolve
    show dv smile pioneer at left
    show us normal pioneer at right
    show mi normal pioneer at center
    with dissolve_fast
    window show dissolve

    dv "Ну что, какие предложения?"

    me "Ты о чем сейчас?"

    dv "О том, что делать будем. {w}Как насчет посиделок в музклубе?"

    show mi shy pioneer at center with dissolve

    "Мику, почему-то, заметно смутилась."

    dv "Ну давайте, соглашайтесь. {w}Подальше от вожатой — подальше от проблем. {w}Как говорится, с глаз долой — из сердца вон."

    window hide dissolve
    stop music fadeout 3
    scene bg black with ov_trise
    $ renpy.pause (1)
    scene bg int_musclub_day with ov_trise
    play ambience ambience_music_club_day
    $ renpy.pause (1)
    window show dissolve

    "До места назначения мы стали пробираться «огородами». {w}На мой закономерный вопрос «зачем?», был получен вполне уверенный ответ - «по теньку идем, чтоб не напекло»."

    "Девочек определенно веселила эта игра в шпионов с ползанием на карачках и прыжками через кусты, я же чувствовал себя великовозрастным идиотом, занимавшимся непонятной ерундой."

    me "Ну, что делать будем? {w}Кроме пряток от вожатой."

    show dv smile pioneer at cleft with dissolve

    dv "А давайте в карты."

    "Остальные присутствующие согласно закивали."

    me "Ну давай."

    "По правде говоря, меня такое предложение несколько огорчило. {w}Я ожидал чего-то более увлекательного. {w}По крайней мере, не такого скучного, как игра в дурака."

    me "А на что играем? {w}Только не говори, на интерес — это скучно."

    show dv grin pioneer at cleft with dissolve

    "По лицу Алисы расплылась фирменная нагловатая ухмылка, от чего внутри тревожно ёкнуло. {w}Но не давать же теперь заднюю."

    dv "Как раз хотела предложить, но, если ты и сам не против… {w}Мику, тащи карты!"

    window hide dissolve
    scene bg black with ov_trise
    $ renpy.pause (1)
    scene bg int_musclub_day with ov_trise
    show dv normal pioneer at fleft with dissolve_fast
    show us normal pioneer at fright with dissolve_fast
    show mi normal pioneer at center with dissolve_fast
    $ renpy.pause (1)
    window show dissolve

    "Мы выстроились кругом вокруг колоды словно костра. {w}В голове мелькнула мысль, что неплохо было бы устроить ночные посиделки с шашлыком под гитару."

    play music ov_summer_ends_soon fadein 4

    th "Луна, потрескивание поленьев в костре, ленивый перебор струн — романтика, эх!"

    show dv normal pioneer2 at fleft with ov_trise

    "Владелица двух желто-оранжевых заколок завязала галстук на запястье, а рубашку узлом, мотивировав это тем, что в комнате жарко."

    me "Так окно открой."

    show dv laugh pioneer2 at fleft with dissolve

    dv "Ты не на меня смотри, а в карты, то, не равен час — продуешь."

    "Она ехидно подмигнула мне. {w}Я ничего не ответил, пытаясь взять себя в руки и оторвать взгляд от полуобнаженной девичьей груди."

    show dv normal pioneer2 at fleft with dissolve

    th "Ох, черт! {w}Держи себя в руках, Семен, держи себя в руках."

    $ renpy.pause (2)

    "Мало того, что у меня не получалось сконцентрироваться на игре, так еще и карточный бог не был ко мне благосклонен, подкинув всего пару мелких козырей за всю игру."

    "Ульяне повезло больше всех: всю игру она терпимо собирала лучшие козырные карты что в итоге привело её к первому месту."

    "Я даже подумывал, не жульничает ли она?"

    "А вот Мику как и мне не очень везло с картами. {w}Но тем не менее она заняла 3 место."

    "И угадайте кто мне всю игру подкидывал карты? {w}Конечно же Алиса. {w}Вот ведь зараза. {w}Кажется она всё специально так подстроила чтобы проиграл именно я."

    "Стоит говорить о том что я проиграл? {w}Я проиграл."

    show dv grin pioneer2 at fleft with dissolve

    dv "Хах, язык мой — враг мой, да Сёма? {w}Ну что, какое ему наказание придумаем?"

    show us grin pioneer at fright with dissolve_fast

    us "А давайте его краской измажем?"

    show us laugh pioneer at fright with dissolve_fast

    extend " Или пусть на площади пять минут петуха изображает. {w}Ходит и кукарекает! {w}Смешно же будет, а?"

    dv "Опять? {w}Не,{w} это уже не интересно."

    show us normal pioneer at fright with dissolve

    us "Ну, Алиса!"

    show dv normal pioneer2 at fleft with dissolve

    dv "Нет, я говорю!"

    $ renpy.pause (1)
    show dv smile pioneer2 at fleft with dissolve

    extend " О, придумала. {w}А давай-ка ты, Семен, рака живого нам принесешь."

    show mi smile pioneer at center
    show us laugh pioneer at fright
    with dissolve

    me "Чего? {w}А динозавра тебе не надо? {w}Где я тебе раков найду?"

    "Рыжая хулиганка, присвистывая, покрутила у виска ладонью, изображая, будто бы подкручивает большой круглый регулятор."

    dv "В реке, Семен, в реке-е. {w}Плавать, как я поняла, ты толком не умеешь. {w}Ладно, горе ты луковое, выдадим тебе маску, авось не утонешь."

    me "Спасибо за напутствие, ты такая заботливая!"

    "Моя интонация буквально сквозила сарказмом. {w}Но Алису это только раззадоривало. {w}Пока мы продолжали препираться, "

    hide us laugh pioneer with dissolve

    extend " Ульяна сбегала за, как я сначала подумал, лыжными очками, но примерив, убедился, что эта вещица и в правду для погружения под воду. {w}Делать было нечего — отвертеться уже не получится. {w}Мы направились к берегу."

    window hide dissolve
    stop music fadeout 3
    stop ambience fadeout 3
    scene bg black with ov_trise
    $ renpy.pause (1.5)
    scene bg ext_beach_day with ov_trise
    play ambience ambience_boat_station_day fadein 2
    $ renpy.pause (1)
    window show dissolve

    "По пути к месту экзекуции меня терзали сомнения, и я попробовал выяснить, что же именно задумала Алиса, а она совершенно точно задумала, моя уверенность была тверда как камень."

    me "Какое-то странное задание, не находишь? {w}Принести из речки рака и всё."

    dv "А ты попробуй его голыми руками из норы достать."

    "Пионерка приветливо улыбалась, то ли мне, то ли нахлынувшим на неё воспоминаниям."

    "Дойдя, наконец, до пункта назначения, я сложил одежду на берегу и в трусах, сбегать за плавками мне не дали, нырнул в реку."

    window hide
    play sound ov_jump_int_water
    scene bg ov_ext_underwater with fla
    $ renpy.pause (0.6)
    window show dissolve

    "Не сказать, чтобы вода была моей стихией — продержавшись около минуты, легкие потребовали новой порции кислорода, и мне пришлось пойти на всплытие с пустыми руками."

    $ renpy.pause (0.7)
    scene bg ext_beach_day with flash

    "Отплевавшись и сняв маску, чтобы почесать глаз, я заметил, что Алиса стоит на берегу, держа подмышкой мою форму и разговаривает с обеспокоенной чем-то Мику."

    "В общем-то, несложно было догадаться, в чем состоял настоящий план рыжей проказницы. {w}Той, что постарше."

    play music ov_doom2_level1 fadein 3
    $ renpy.pause (1)

    "Ничего не оставалось, как поспешить на берег. {w}Увидев мое серьезное выражение лица, Алиса заулыбалась и, заливисто рассмеявшись, побежала в сторону музклуба."

    "Внутри начала закипать злость, кулаки непроизвольно сжались, а в голове прокручивались все знакомые мне ругательства, в слух которые лучше не произносить."

    window hide dissolve
    stop ambience fadeout 1
    scene bg ext_beach_day at running with flash
    $ renpy.pause (1.5)
    scene bg ext_houses_day at running with flash
    $ renpy.pause (1)
    window show dissolve

    "Ноги, страдающие от отсутствия обуви, нещадно ныли, никак не способствуя успокоению."

    scene bg ext_musclub_day with flash:
        xalign 0.53 yalign 0.53 zoom 1.25

    "Добежав до спасительного «стеклянного домика», пионерка попыталась закрыться, удерживая дверь, упершись в стену ногами, наивно полагая, что это меня остановит. {w}Но клокочущие внутри эмоции были настолько сильными, что когда я дернул рукой за ручку, Алиса чуть было не вывалилась наружу."

    "Быстро поняв, что в честном перетягивании лагерного имущества ей не победить, она на очередной моей попытке войти в музклуб отпустила злополучную дверь, и она очень «удачно», с размахом, прилетела мне в лоб."

    with flash_red

    "Я взвыл от боли и зарычал, вбегая в остекленный зал с деревянными полами."

    window hide
    scene bg black with dissolve
    $ renpy.pause (1.5)
    scene bg int_musclub_day with dissolve
    window show dissolve

    "Алиса заскочила за рояль и, все так же весело смеясь, показала мне язык. {w}Глаза её горели, излучая щенячий восторг, щеки налились румянцем, а прическа растрепалась. {w}Я, видя, что добыча загнана в угол, нарочито медленно подошел к, покрытому черным лаком, концертному инструменту."

    me "Попалась. {w}Отдай форму."

    "В ответ мне скривили рожицу и в очередной раз продемонстрировали язык. {w}Алиса попыталась проскочить мимо меня, сделав несколько ложных выпадов влево-вправо, но я оказался проворнее и сумел в последний момент ухватить её за локоть. {w}Плохо соображая в горячке погони, я бросил рыжую забияку на рояль и, сжав в объятиях, придавил всем телом."

    "Поймав-таки свою добычу, я растерялся, не зная что теперь делать, чем попыталась воспользоваться Алиса, но захват оказался всё еще слишком крепким, и ей не удалось выбраться из него."

    "Неожиданно в комнату вошли и окатили веселым криком:"

    us "Семен, решил-таки кого-нибудь снасильничать?!" with hpunch

    stop music fadeout 4

    "От неожиданности я, оборачиваясь на звук, отпустил Алису, и она тут же отскочила на безопасное расстояние с возгласом:"

    dv "Не догонишь, не догонишь бе-е-е!"

    play music ov_misato_theme fadein 3

    "Следом в музклуб зашла Мику. {w}Увидев меня, она пискнула, покраснела и прикрыла рот ладошкой."

    "Логично предполагая, что что-то явно не так, мне пришло в голову осмотреть себя. {w}В общем, оказалось, что я стоял посреди комнаты в одних мокрых трусах с, словно рыцарь, копьем на изготовку."

    "Мне стало жутко неловко."

    $ renpy.pause (0.7)
    show mi dontlike pioneer at right with ov_trise

    mi "Ну Алис, отдай уже форму Сеньке, а то Ольга Дмитриевна придёт…"

    show dv normal pioneer2 at left with ov_trise

    dv "Скучные вы."

    hide dv normal pioneer2 with ov_trise

    "Алиса отдала мне форму."

    show mi normal pioneer at right with ov_trise

    me "Эх, теперь ноги болят{cps=3}...{/cps}."

    me "Пойду я лучше посплю, желательно часика два."

    show us laugh pioneer at left with dissolve

    "Ульяна улыбнулась во все тридцать три зуба."

    us "Часика два говоришь? {w}Обед скоро, дурак."

    us "Так что если будешь меньше болтать то больше времени {i}отдОхнёшь{/i}."

    window hide dissolve
    stop music fadeout 5
    scene bg black with dissolve2
    $ renpy.pause (2.5)
    play ambience ambience_dining_hall_full fadein 4
    scene int_dining_hall_people_day with ov_trise
    $ renpy.pause (0.6)
    window show dissolve

    "Тем временем я переоделся, при этом даже успев немного вздремнуть, однако сон сном, но обед по расписанию, так что сразу же после горна ваш покорный слуга пошел набивать желудок."

    th "Что-то девочек не видно{cps=3}...{/cps}. {w}Ну ладно, без них поем."

    $ renpy.pause (1.5)

    "Простояв некоторое время в очереди, я всё-таки ухватил свою законную порцию пищи и сел за первый свободный столик."

    th "Макарошки, сосиски и компот. {w}Очень хорошо по сравнению с завтраком."

    "Я, решив не медлить, принялся за еду."

    $ renpy.pause (2)

    "Доев порцию, я вышел из столовой."

    window hide dissolve
    play sound sfx_open_cabinet_1
    stop ambience fadeout 3
    scene bg black with ov_trise
    $ renpy.pause (1.5)
    play music ov_darker_than_black fadein 4
    scene bg ext_square_day with ov_trise
    $ renpy.pause (1)
    window show dissolve

    th "Итак, девочки до сих пор не объявились. {w}Куда бы податься? {w}Хмм…"

    th "Может просто найти их самому? {w}Где они могут быть? {w}Первым делом конечно же стоит проверить музыкальный клуб."

    window hide dissolve
    scene bg black with dissolve
    $ renpy.pause (1)
    scene bg ext_musclub_day with dissolve
    window show dissolve

    th "Дверь закрыта. {w}Интересно…"

    window hide dissolve
    scene bg black with dissolve
    $ renpy.pause (1)
    scene bg ext_square_day with dissolve
    $ renpy.pause (0.6)
    window show dissolve

    th "Так ладно, я все понял. {w}Буду развлекать себя сам."

    th "Хвала Аллаху хотят бы за то что Дмитриевну не видно, иначе бы точно остатки вечера таскал сахар."

    "Вдруг резко я заострил внимание на памятнике Генды."

    scene bg ext_square_day:
        ease 1.0 xalign 0.5 yalign 0.48 zoom 1.25
    $ renpy.pause (2.5)

    th "Генда…{w} Очень знакомое имя, будто я слышал его в {i}старом{/i} мире."

    th "Всё страньше и страньше… {w}Этот лагерь точно сведёт меня с ума."

    scene bg ext_square_day:
        xalign 0.5 yalign 0.48 zoom 1.25
        ease 1.0 xalign 0 yalign 0 zoom 1
    $ renpy.pause (2.5)

    #PART II

    th "Хмм, а кто там идёт?"

    show cs normal far at left with dissolve

    th "Это же наша медсестра."

    show cs normal far at center with moveinright
    pause 0.1
    show cs normal with dissolve

    me "Здравствуйте, Виола."

    "Виолетта остановилась."

    $ renpy.pause (1.3)
    show cs smile at center with dissolve

    cs "Здравствуй, {i}пионер{w=0.2}-р{/i}. {w}Чем обязана?"

    "Сказала она в своей привычной манере."

    me "Куда путь держите? {w}В первый раз вижу вас за пределами медпункта."

    show cs normal at center with dissolve

    cs "К Оле. {w}На чаепитие пригласила."

    $ renpy.pause (1.3)

    cs "{b}Кстати{/b},{w} Семён. {w}Раз я тебя встретила, будь другом, посиди часик в медпункте за меня? {w}Если кто придёт, дашь ему все, что понадобится."

    th "Ага. {w}Значит, пока они {i}чай пьют{/i} я в медпункте сидеть буду? {w}Ладно-ладно, я запомнил!"

    $ renpy.pause (1.1)

    th "С другой стороны{w=1}, делать мне все равно нечего,{w=1} так что почему бы и нет?"

    th "Впрочем, я не думаю, что с моим приходом в медпункт, здоровье у остальных пионеров резко пошатнется."

    me "Без проблем."

    show cs smile at center with dissolve

    cs "Вот и молодец. {w}Держи ключи. {w}А я пошла. {w}Если что-то серьезное – зови. {w}Удачи, {b}пионер{w=0.3}-р{/b}."

    "По коже пробежались мелкие мурашки."

    th "Ох, Виола, Виола. {w}Ты меня сведёшь с ума быстрее чем  этот мистический лагерь."

    me "Хорошо. {w}Удачного чаепития."

    "Виола подмигнула мне."

    "И растворилась."

    hide cs smile with moveoutright

    th "Ну-с, отправляемся."

    window hide dissolve
    scene bg black with fade
    stop music fadeout 3
    play ambience ambience_medstation_inside_day fadein 3
    $ renpy.pause (1.7)
    scene bg int_aidpost_day with dissolve2
    $ renpy.pause (2.0)
    window show dissolve

    th "Так, теперь я временная медсес{cps=3}...{/cps}"

    with hpunch

    th "Тьфу, временный {b}медбрат{/b} получается."

    th "Посмотрим, что у Виолы тут есть. {w}Журналы, может, какие…?"

    "Я открыл ящики и начал рыться в них."

    th "{i}Дело нечестивое, милорд, но и я не настоящий пионер! {w}Вы не джентльмен. {w}А вы, мисс, не леди, но я вас в этом не обвиняю, леди мне никогда не нравились.{/i} "

    th "Анальгин,{w} бинты,{w} {i}презервативы,{w} тампоны,{/i}{w} {b}о{/b}! {w}А вот и журнальчик."

    th "«Советская мода». {w}Почему я не удивлен?"

    "Нет. {w}Этот журнал не мог представлять никакого для меня интереса, разве что на нем должна быть указана дата выпуска."

    "Я внимательно изучил обложку."

    "Ничего. {w}Только месяц и число – 21 июля."

    "На обратной стороне тоже самое."

    th "{b}Да что же это такое!{/b}"

    "Я с досады кинул журнал обратно на стол."

    th "Ладно, ничего страшного… {w}Ответы от меня никуда не убегут, {i}у них же нет ушей, лап и хвоста!{/i}"

    th "В любом случае мода Союза меня не интересует."

    "Снова открыв ящики и убрав «Советскую моду» обратно в полку, я упал лицом на кушетку."

    th "Удобно, в таком положении и заснуть можно."

    $ renpy.pause (1)

    "Я бы и лежал так, но в дверь постучались."

    play sound sfx_knock_door7_polite

    "Лениво сказав «Войдите», в комнату зашла…"

    show un normal pioneer with moveinright

    "Лена."

    show un surprise pioneer with dissolve

    un "Ой,{w=0.3} а медсестры нет? {w}Тогда я позже зайду."

    me "Привет Лен, я за медсестру. {w}Чем могу помочь?"

    show un grin pioneer with dissolve
    $ renpy.pause (1.0)

    un "П{w=0.2}-привет, ну {cps=3}...{/cps}"

    $ renpy.pause (1)

    "Лена замялась, но всё же ответила:"

    show un shy pioneer with dissolve

    un "Можешь, пожалуйста, выйти на минуточку?"

    me "Эм, хорошо."

    show un grin pioneer with dissolve

    "Решив не задавать лишних вопросов, я деликатно вышел подышать воздухом."

    window hide dissolve
    scene bg black with dissolve
    play ambience ambience_camp_center_day
    play sound sfx_slavya_gets_out
    $ renpy.pause (1.4)
    play sound sfx_close_cabinet
    scene bg ext_aidpost_day with dissolve
    $ renpy.pause (2.0)
    $ renpy.pause (1)
    window show dissolve

    "Ждать долго не пришлось{w} – пионерка вышла через пару минут с пакетом в руках."

    show un smile pioneer with dissolve

    un "Спасибо."

    me "Обращайся!"

    hide un smile pioneer with moveoutright

    "Дождавшись, когда она спустится с крыльца, я,{w} нелепо кивнув, вернулся в медпункт."

    window hide dissolve
    scene bg black with dissolve
    $ renpy.pause (1.4)
    play ambience ambience_medstation_inside_day fadein 3
    scene bg int_aidpost_day with dissolve
    $ renpy.pause (2.0)
    window show dissolve

    "Прошёл час. {w}За это время практически никто не заходил, кроме…"

    window hide dissolve
    scene image im.MatrixColor(get_image("bg/int_aidpost_day.jpg"), im.matrix.desaturate())
    with flash

    play music fl_lgg_symphonical_rain_willfully fadein 3

    show image im.MatrixColor( im.Composite((900,1080), (0,0), get_image("sprites/normal/dv/dv_2_body.png"),(0,0), get_image("sprites/normal/dv/dv_2_pioneer2.png"),(0,0), get_image("sprites/normal/dv/dv_2_grin.png")), im.matrix.desaturate()) as dv
    with dissolve2
    window show dissolve

    dv "О, {i}Сёмыч{/i}. {w}Привет, а я тебя искала! {w}Что ты тут делаешь?"

    me "Искала? {w}Не заметно! {w}Это я вас искал! {w}В итоге не нашёл, и Виола меня попросила посидеть в медпункте. {w}Сижу теперь, от скуки подыхаю. {w}{i}Журналы женские читаю{/i}."

    "Лицо Алисы продолжало выражать свойственную ей надменную ухмылку."

    me "Искала,{w=0.5} говоришь,{w=0.5} а зачем?"

    dv "Ясненько. {w}Ну{cps=3}...{/cps} просто я хочу Генду взорвать."

    me "{cps=3}...{/cps}"

    "Будь я истинным пионером,{w} я бы взмахнул руками и отчитал Алису как следует{w} {s}(благополучно получив в глаз){/s}{w} Но в реальности-то я взрослый{w}, если так можно сказать{w}, {i}мужик{/i}."

    me "Во-первых,{w=1} где ты собираешься найти бомбу? {w}Во-вторых,{w=1} тебе утреннего наказания от нашей вожатой было мало?"

    "Я вспомнил виды мокренькой Алисы."

    th "{b}Не время!{/b}"

    $ renpy.pause (1)

    me "В-третьих, {w=1} причём тут я, и что от меня требуется? {w}И да, что тебе сделал {i}многоуважаемый товарищ Генда{/i}?"

    dv "Не будь занудой. {w}Всё будет нормально. {w}Бомбу я сделаю, сама. {w}Вернее, уже сделала. {w}Почти. {w}От тебя требуется только уголь. {w}Активированный. {w}А если ты наша {i}медсестра{/i}, то это упрощает дело в разы!"

    "Алиса широко улыбнулась."

    me "Эх, а ведь изготовление взрывчатых веществ преследуется по закону… {w}Да и с чего ты взяла, что я его тебе дам? {w}Мне проблемы не нужны!"

    dv "Да какое взрывчатое вещество? {w}С дуба рухнул? {w}Это же как петарда, совсем слабенькая бомбочка. {w}Семён, ну и долгий же ты. {w}Полный тормоз. {w}Просто дай мне уголь и все. {w}Представим, что у меня болел живот. {w}Хоть это ты умеешь?"

    me "Дать,{w=0.3} или не дать? {w}Вот в чём вопрос!"

    show image  im.MatrixColor( im.Composite((900,1080), (0,0), get_image("sprites/normal/dv/dv_5_body.png"),(0,0), get_image("sprites/normal/dv/dv_5_pioneer2.png"),(0,0), get_image("sprites/normal/dv/dv_5_angry.png")), im.matrix.desaturate()) as dv
    with dissolve

    dv "Мое терпение не железное!"

    show image  im.MatrixColor( im.Composite((900,1080), (0,0), get_image("sprites/normal/dv/dv_5_body.png"),(0,0), get_image("sprites/normal/dv/dv_5_pioneer2.png"),(0,0), get_image("sprites/normal/dv/dv_5_angry.png")), im.matrix.desaturate()) as dv:
        ease 3 zoom 1.6 yoffset adapt_hotspot(400)

    me "Да шучу, шучу! {w} Если что, {w=0.3}я не виноват. {w}Под твою ответственность."

    show image im.MatrixColor( im.Composite((900,1080), (0,0), get_image("sprites/normal/dv/dv_4_body.png"),(0,0), get_image("sprites/normal/dv/dv_4_pioneer2.png"),(0,0), get_image("sprites/normal/dv/dv_4_normal.png")), im.matrix.desaturate() ) as dv with dissolve
    show image im.MatrixColor( im.Composite((900,1080), (0,0), get_image("sprites/normal/dv/dv_4_body.png"),(0,0), get_image("sprites/normal/dv/dv_4_pioneer2.png"),(0,0), get_image("sprites/normal/dv/dv_4_normal.png")), im.matrix.desaturate() ) as dv:
        ease 1 zoom 1.0  yoffset 0

    "Сказал я, будучи заранее уверенным в том, что меня выставят чуть ли не зачинщиком."

    "Я достал из шкафчика уголь и подал его Алисе."

    dv "Спасибо! {w}Да, да. {w}Под мою ответственность, {i}бла-бла-бла{/i}."

    window hide dissolve
    scene bg int_aidpost_day with flash
    stop music fadeout 2
    play ambience ambience_medstation_inside_day fadein 3
    $ renpy.pause (2.0)
    window show dissolve

    th "Вот ведь рыжая бестия!"

    "Но на моей лице не было места досаде, только легкая улыбка."

    th "Нет, влюбиться мне ещё не хватало. "

    th "Семён. {w}Держи себя в руках, ты помнишь чем это кончается, верно? {w}Пора взрослеть."

    "Воспоминания горькими комьями подступили к глотке…."

    window hide dissolve
    stop ambience fadeout 2
    $ renpy.pause (2.0)
    scene image im.MatrixColor(get_image("bg/bus_stop.jpg"), im.matrix.desaturate()) with flash
    play music fl_maxim_n_relax fadein 4
    $ prolog_time()
    $ renpy.pause (3.0)
    window show dissolve

    "Зима какого-то там года. {w}Остановка. {w}Мы с одногруппницей ждём маршрутки."

    "На тот момент я был в неё влюблен. {w} С самого начала, стоило мне только поступить на юридический. {w}Но смелостью я никогда не славился, потому просто ждал. {w}Чего? {w}Непонятно."

    window hide dissolve
    scene image im.MatrixColor(get_image("anim/intro_9.jpg"), im.matrix.desaturate()) with dissolve
    play sound sfx_intro_bus_engine_start
    $ renpy.pause (1.0)
    window show dissolve

    "Мой 410 автобус подъехал, я уже собрался заходить в маршрутку и говорить «пока», но {cps=3}...{/cps}"

    voice "Семён,{w=0.5} подожди!"

    me "А?"

    th "Какое совпадение, не правда ли? {w}Может, это лагерь смеется надо мной, подкидывая дров в иллюзорный камин моих воспоминаний? {w}Местная Лена напоминает мне {b}МОЮ{/b} Лену,{w} есть в пионерке что-то ярко и броское, чего я ещё не до конца понял, но к чему уже тянусь, как дитя."

    $ renpy.pause (1.0)
    scene image im.MatrixColor(get_image("bg/bus_stop.jpg"), im.matrix.desaturate()) with dissolve2
    play sound sfx_bus_out

    "Я резко остановился. {w}410 уехал прямо перед моим носом."

    voice "Это,{w=1} наверное,{w=1} глупо,{w=1} но… {w}Я,{w=1} ты…"

    "Она резко выдохнула и сказала те самые слова:"

    voice  "{color=#C09}Ты мне нравишься!{/color} {w}Очень! {w}Ты хочешь со мной встречаться?"

    "Мир перевернулся и улетел в урну бытия."

    "Остался только я и она."

    "Мои щёки покраснели. {w}То ли из-за мороза, то ли…"

    me "Ты {cps=3}...{/cps} Тоже мне нравишься!"

    voice "Правда?"

    window hide dissolve
    show blink
    pause 2
    scene bg black
    $ renpy.pause (1)
    window show dissolve

    "Тогда мы поцеловались. {w}Мой первый поцелуй. {w}Самый лучший. {w}Кроме него не существовало ничего. {w}Совсем."

    window hide dissolve
    $ renpy.pause(1)
    nvl clear
    $ set_mode_nvl()
    window show dissolve2

    "В последствии мы начали встречаться. {w}А потом она даже переехала ко мне. {w}После окончания колледжа мы должны были сыграть свадьбу, но…"

    $ renpy.pause (2.0)

    "\n\n\nЛена погибла. \n\n{w}{b}Какой-то пьяный мудак{/b} вылетел на встречную полосу!"

    "\nДо сих пор помню свою реакцию, когда я об этом узнал:"

    "Я слушал,{w=1} слушал,{w=1} как мне говорят, что она сегодня не приедет домой."

    window hide dissolve
    stop music fadeout 3
    pause 0.8
    play music fl_maxim_n_concern fadein 6
    window show dissolve

    "\nПосле прошли её похороны,{w=1} на которые я не пришел. {w}Иначе бы прямо там потребовал положить меня рядом с ней."

    window hide dissolve2
    $ renpy.pause (3.0)
    scene image im.MatrixColor(get_image("bg/semen_room.jpg"), im.matrix.desaturate()) with fade3
    nvl clear
    window show dissolve

    "Время шло. {w}Все меньше вещей интересовало меня, я бросил универ на четвертом курсе. {w}Начал замыкаться в себе."

    "\nТо и дело мелькали прозрачные как вода мысли о самоубийстве, но я их всегда быстро отгонял. {w}Не хотелось, чтобы мое синее тело нашли в лужах."

    "На звонки родных и друзей не отвечал. {w}Зачем? {w}Опять слышать эти ваши моральные поддержки?"

    "\n\n\nСпустя 3 года мне позвонил одногруппник и пригласил на встречу выпускников. {w}Полчаса прериканий – и он меня уговорил. {w}Не знаю, почему я вдруг согласился… {w}В моей голове билась мысль, что я должен поехать туда. {w}Несомненно."

    window hide dissolve2
    scene image im.MatrixColor(get_image("bg/bus_stop.jpg"), im.matrix.desaturate()) with dspr
    $ renpy.pause (3.0)
    window show dissolve

    "\nЯ пришёл на ту самую остановку. {w}Сердце отыгрывало панковский мотив прямо в висках, на глаза невольно навернулись слезы."

    window hide dissolve2
    scene image im.MatrixColor(get_image("anim/intro_9.jpg"), im.matrix.desaturate()) with dspr
    $ renpy.pause (3.0)
    play sound sfx_intro_bus_door_open
    window show dissolve

    "\n\nНо я, всё же, сел на 410… {w}Сел, и оказался {cps=3}...{/cps}"

    window hide dissolve
    $ renpy.pause (1.5)
    scene image im.MatrixColor(get_image("anim/intro_13.jpg"), im.matrix.desaturate()) with dspr
    play sound sfx_bus_out
    $ renpy.pause (2.0)
    scene bg black with fade3
    nvl clear
    $ set_mode_adv()
    $ renpy.pause (3.0)
    stop music fadeout 3
    play ambience ambience_medstation_inside_day fadein 3
    scene bg int_aidpost_day with flash
    $ renpy.pause (2.0)
    $ day_time()
    window show dissolve

    me "…здесь."

    play sound sfx_dinner_jingle_speaker

    "Мои мысли прервал горн на ужин."

    th " Ну наконец-то! {w}Теперь у меня есть уважительная причина покинуть медпункт! {w}Уже прошло полтора часа, так что пора бы Виоле вернуться. "

    window hide dissolve
    play sound sfx_medpunkt_door_open
    $ renpy.pause (3.0)
    show cs smile at center
    with dissolve2
    $ renpy.pause (2.0)

    th "Помянешь черта…"

    cs "Спасибо тебе большое, {i}пионер{w=0.6}-р{/i}. {w}Кто-нибудь заходил?"

    me "Да, Лена и Алиса."

    show cs normal at center
    with dissolve

    cs "И что с ними?"

    me "Ничего серьезного."

    cs "Хорошо, тогда ты можешь быть свободен."

    me "Ключи, если что, я оставил на кушетке! {w}До свидания!"

    show cs smile at center

    cs "До свидания. {w}Ещё раз спасибо тебе."

    "Я вышел из медпункта."

    window hide dissolve
    scene bg black
    stop ambience fadeout 2
    with fade3
    $ renpy.pause (2.0)
    scene bg ext_dining_hall_near_day
    with fade
    $ renpy.pause (2.0)
    window show dissolve2

    th "Единственное, чему я рад в независимости от обстоятельств, так это столовой. {w}Хоть какая-то, но еда"

    window hide dissolve
    $ renpy.pause (1)
    scene bg int_dining_hall_people_day
    with dspr
    play ambience ambience_dining_hall_full fadein 3
    $ renpy.pause (2.0)
    window show dissolve2

    "Свободное место было только рядом со Славей и Женей."

    show mz bukal glasses pioneer at cleft
    show sl smile pioneer at cright
    with dissolve
    $ renpy.pause (1.5)

    me "Я присяду?"

    sl "Конечно, садись!"

    "Женя согласно кивнула, не отвлекаясь от еды."

    $ renpy.pause (1)
    show sl normal pioneer at cright
    with dissolve
    $ renpy.pause (1)

    sl "Как ты? {w}Надеюсь, что лучше?"

    me "Да, намного. {w}Спасибо за беспокойство, Славя."

    show sl smile pioneer at cright
    with dissolve
    $ renpy.pause (1.3)
    hide sl smile pioneer
    hide mz bukal glasses pioneer
    with dissolve

    "Сегодня на ужин у нас картофельное пюре с  котлетой и бледной долькой помидора. {w}Запивать эти чудеса придется сладким чаем. {w}Не так уж и плохо…"

    $ renpy.pause (1)

    "Я огляделся, пытаясь найти рыжие хвостики среди пионеров… {w}Бесполезно."

    "Под мерный гул алюминиевых ложек и разговоры Слави с Женей я спешно съел свой ужин, и залпом запил его чаем. {w}Не хотелось задерживаться здесь надолго. {w}Не знаю почему, но очень уж сильно оно давит мне на мозги."

    $ renpy.pause (1)
    show sl normal pioneer at cright
    show mz bukal glasses pioneer at cleft
    with dissolve

    me "Ну, до встречи, девушки!"

    show sl smile pioneer at cright with dissolve
    $ renpy.pause (1)
    hide sl smile pioneer
    hide mz bukal glasses pioneer
    with dissolve

    "Я встал, отнёс грязную посуду и вышел из столовой."

    window hide dissolve
    stop ambience fadeout 2
    $ renpy.pause (1)
    scene bg ext_dining_hall_near_sunset
    with fade3
    play ambience ambience_camp_center_day fadein 3
    $ sunset_time()
    $ persistent.sprite_time = 'sunset'
    $ renpy.pause (2.0)

    "Ничего не предвещало беды, как вдруг {cps=3}...{/cps}"

    play sound sfx_muffled_explosion
    pause 1
    play music music_list["pile"] fadein 1.5

    "Как раздался громкий хлопок со стороны площади!"

    th "Ёк-макарек{w}, {b}Алиса{/b}, чёрт бы тебя подрал!"

    window hide dissolve
    scene bg ext_square_sunset with dspr
    window show dissolve

    "Генда, и без того серый, сейчас покрылся черной дымкой."

    "На площади кроме нас были и другие пионеры, а также Ольга Дмитриевна."

    show mt rage pioneer far with dissolve

    mt "{b}Где Двачевская?!{/b}"

    "Вожатая говорила одновременно со всеми, но, в то же время, ни с кем."

    $ renpy.pause (2.0)

    "И тут ее взгляд поймал меня."

    show mt angry pioneer with dissolve
    $ renpy.pause (1)

    mt "{b}Семён{/b}, ты не видел Алису?"

    me "Нет, не видел."

    th "К сожалению."

    mt "Уверен?"

    me "Честное пионерское!"

    show mt shocked pioneer with dissolve
    $ renpy.pause (1.4)

    "Ольга внимательно на меня посмотрела."

    $ renpy.pause (2.0)
    show mt smile pioneer with dissolve
    stop music fadeout 2

    "И улыбнулась."

    play music music_list["smooth_machine"] fadein 3

    mt "Смотри мне."

    show mt normal pioneer with dissolve

    extend " И где же мне её искать? {w}Ульяна, подойди ко мне!"

    $ renpy.pause (2.0)
    show us grin sport at cright with dissolve2
    $ renpy.pause (1)

    us "Что такое Ольга Дмитриевна?"

    show mt angry pioneer with dissolve

    mt "Ты сама знаешь."

    show us normal sport at cright with dissolve

    us "Ой, можно подумать, можно подумать."

    $ renpy.pause (1)
    show us smile sport at cright with dissolve

    extend " Не знаю я."

    show us normal sport at cright with dissolve

    us "В последний раз мы виделись после обеда. {w}Остальное время я играла в футбол. {w}На ужин я не ходила. {w}Может она там была?"

    show mt normal pioneer with dissolve

    mt "Ясно всё."

    $ renpy.pause (1)

    "Голос" "{i}Вот она!{/i}"

    "Кто-то крикнул в толпе, и действительно: Алиса собственной персоной стояла посреди этого хауса с ухмылкой. {w}Кажется её эта ситуация очень сильно забавила."

    $ renpy.pause (0.5)
    show dv laugh pioneer2 far at fleft with dissolve2
    $ renpy.pause (2.0)
    show mt angry pioneer with dissolve

    mt "{b}Алиса!{/b} И зачем?"

    $ renpy.pause (2.0)
    show dv laugh pioneer2 at left with dissolve2
    $ renpy.pause (0.4)
    hide us normal sport with moveoutright

    "В Ульяна в свою очередь уже ускользнула, неужели всё-таки Алисе помогала?"

    $ renpy.pause (1)

    dv "Весело же!"

    show mt angry pioneer with dissolve

    mt "Весело будет, когда ты будешь отмывать памятник прямо сейчас и всю ночь, если потребуется."

    show dv sad pioneer2 at left with dissolve

    dv "Ладно, ладно. {w}Я поняла. {w}Извините."

    show mt angry pioneer with dissolve

    mt "Бомбу из чего делала?"

    show dv guilty pioneer2 at left with dissolve

    dv "Сера, селитра и активированный уголь!"

    $ renpy.pause (1)

    "Вдруг Алиса резко оживилась и показала на меня пальцем."

    show dv grin pioneer2 at left with dissolve_fast

    dv "А уголь дал мне Семён."

    show mt shocked pioneer with dissolve

    th "{b}ЧТО???{/b}"

    th "Впринципе подобного и нужно ждать от Алисы."

    "Я не нашелся что ответить, ведь технически это правда."

    $ renpy.pause (1)

    show mt laugh pioneer with dissolve_fast

    "Неожиданно вожатая рассмеялась."

    mt "Идите уже,{w} террористы юные,{w} завтра чтобы Генда блестел."

    "Сказав это, Ольга ушла."

    hide mt laugh pioneer with dissolve2
    show dv smile pioneer2 at center with moveinright
    "На площади остались только я и Алиса."

    show dv laugh pioneer2 at center with dissolve

    dv "Семён, ну ты бы видел свое лицо."

    "Алиса рассмеялась."

    me "{b}Ну Алиса!{/b}"

    show dv smile pioneer2 at center with dissolve

    dv "А что я?"

    me "Я тебе помог… {w}А ты…"

    show dv grin pioneer2 at center with dissolve

    dv "Не кисни, Сёмыч. {w}Ты нам ещё нужен."

    me "Для издевательств?"

    show dv smile pioneer2 at center with dissolve

    dv "Да."

    "Я улыбнулся."

    me "Садистка. {w}Тебя бы по одному месту ремнём шлёпнуть."

    show dv laugh pioneer2 at center with dissolve

    "Алиса показала язык."

    hide dv laugh pioneer2 with moveoutleft

    "И убежала."

    "А я так и остался стоять на площаде с придурковатой улыбкой. {w}Зачем?"

    "Не знаю."

    window hide dissolve
    scene bg black with fade3
    play music fl_wolf_tails_cool fadein 3
    $ renpy.pause (2.0)
    window show dissolve

    "Спустя некоторое время прозвучал сигнал на отбой."

    "Я, пока ещё довольный и уставший, лишь медленно ковылял в нужном направлении."

    "Это место придавало мне какое-то невообразимое количество сил. {w}Да, суперменом я явно не стал, но и это куда лучше того, что происходило со мной дома."

    window hide dissolve2
    scene bg ext_house_of_mt_night_without_light
    with dspr
    play sound sfx_open_door_1
    $ renpy.pause (1.7)
    scene bg int_house_of_mt_night2
    with fade2
    $ renpy.pause (1)
    scene bg int_house_of_mt_night
    with dissolve
    $ renpy.pause (1)
    window show dissolve

    "Пройдя во внутрь, я быстро разделся, как можно аккуратней бросив вещи на спинку стула, и лег под одеяло."

    "Оставшийся свет от лампочки меня совсем не волновал."

    "День был настолько разнообразным на события, на чувства и эмоции, что, когда мое тело коснулось мягкой кровати, я уже ни за что не хотел вставать. {w}Вот придет вожатая, пусть займется."

    th "Этот день напомнил мне о  Лене…. {w}МОЕЙ ЛЕНЕ… {w}Казалось, что это все замкнулось где-то в прошлом, в таком далеком, что мне оставалось лишь удивляться жестокости такого явления, как время, которое с постоянной дотошностью указывает тебе на ее собственную цикличность."

    th "Есть ещё и Славя. {w}Очень заботливая, хорошая, красивая… {w}Отличная пионерка. {w}Рядом с ней уютно и тепло."

    th "И в это же время «Совенок» показал мне Алису…Ту, из-за которой я, скорее всего буду наказан. {w}Возможно, в этом, пока что коротком, путешествии она ещё успеет преподнести мне пару-тройку сюрпризов."

    play sound sfx_open_door_2
    $ day_time()
    $ persistent.sprite_time = 'day'
    pause 2
    show mt angry pioneer with dissolve_fast

    mt "А он уже спит! {w}Как со всякими недобросовестными элементами сотрудничать – силы есть! {w}А как ответственность нести – баста"

    "Ольга Дмитриевна помолчала мгновение, а потом ее гнев сменился на сонную милость."

    show mt normal pioneer with dissolve

    mt "Завтра с вами разберусь!"

    window hide dissolve
    hide mt normal pioneer with dissolve
    $ renpy.pause (1)
    # щелк #П.П а звука то нет
    scene bg int_house_of_mt_night2
    with dissolve_fast
    stop music fadeout 3
    $ renpy.pause (1)
    show blink
    pause 3
    scene bg black
    with dissolve
    jump flew_third_day
